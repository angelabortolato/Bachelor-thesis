set xrange [0:200]
set yrange [0:200]
unset xtics
unset ytics
set xlabel "neuroni"
set ylabel "neuroni"
set palette defined ( 0 "blue", 1 "white", 2 "red" )
set cbtics 0.25
unset key
set title noenhanced 
set terminal pngcairo enhanced font "Arial,18.0" size 640,480

do for [i=0:19] {set output sprintf('Jstep%d.png', i);set title sprintf('Matrice di connessione, trial %d', i*500); plot sprintf("Jstep%d.txt", i) matrix w image notitle}		
set output sprintf('Jstep%d.png', 20)
set title sprintf('Matrice di connessione, trial %d', 10000)
plot "Jfin.txt" matrix w image notitle	


set xrange [-0.5:0.5]
set yrange [0:*]
set xtics auto
set ytics auto
set xlabel "Peso"
set ylabel "Frequenza"
set style histogram gap 0

normal(x, mu, sd) = 1760*(1/(sd*sqrt(2*pi)))*exp(-(x-mu)**2/(2*sd**2))
do for [i=0:20] {set output sprintf('histoJstep%d.png', i);set title sprintf('Distribuzione pesi, trial %d', i*500); plot sprintf("histJstep%d.txt", i) using 1:2 w boxes notitle, normal(x,0,0.106) w l notitle}		
#set output sprintf('histoJstep%d.png', 20)
#set title sprintf('Distribuzione pesi, trial %d', 10000)
#plot "histJfin.txt" using 1:2  with boxes notitle, normal(x,0,0.106) w l notitle		
	


set terminal pngcairo enhanced font "Arial,18.0" size 840,480

unset title
set output 'mostchangingweights.png'
set key outside c r
set x2label "Numero di trial"
unset xlabel
set ylabel "Peso"
set xtics 2000
set ytics auto
set autoscale
plot 'step_changeweights.txt' u 1:2 w l t "J_{31,67}", '' u 1:3 w l t "J_{57,6}", '' u 1:4 w l t "J_{57,112}", '' u 1:5 w l t "J_{156,124}", '' u 1:6 w l t "J_{185,103}", '' u 1:7 w l t "J_{154,39}", '' u 1:8 w l t "J_{185,48}", '' u 1:9 w l t "J_{185,146}", '' u 1:10 w l t "J_{195,126}", '' u 1:11 w l t "J_{195,194}"

set output 'mostchangingweightsZOOM.png'
set xrange [0:2000]
set xtics 400
plot 'step_changeweights.txt' u 1:2 w l t "J_{31,67}", '' u 1:3 w l t "J_{57,6}", '' u 1:4 w l t "J_{57,112}", '' u 1:5 w l t "J_{156,124}", '' u 1:6 w l t "J_{185,103}", '' u 1:7 w l t "J_{154,39}", '' u 1:8 w l t "J_{185,48}", '' u 1:9 w l t "J_{185,146}", '' u 1:10 w l t "J_{195,126}", '' u 1:11 w l t "J_{195,194}"


set terminal pngcairo enhanced font "Arial,18.0" size 640,480

set output 'globalerror.png'
set autoscale
set key inside t r
set xlabel 'Numero di trial'
set ylabel 'Errore'
unset x2label
set xtics 2000
plot "currerr.txt" with points pointtype 7 ps 0.5 notitle

set output 'errorpertype.png'
set xtics auto
plot "currerr.txt" every 4::0 with points pointtype 7 ps 0.5 t 'Tipo trial AA', "" every 4::1 with points pointtype 7 ps 0.5 t 'Tipo trial AB', "" every 4::2 with points pointtype 7 ps 0.5 t 'Tipo trial BA', "" every 4::3 with points pointtype 7 ps 0.5 t 'Tipo trial BB'






